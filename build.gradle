plugins {
    id 'net.researchgate.release' version '3.0.0'
    id 'java'
    id  'maven-publish'
    id 'signing'
    id("io.github.gradle-nexus.publish-plugin") version "1.1.0"
}

group = 'fr.maif'

repositories {
    mavenCentral()
}

java {
    withSourcesJar()
    withJavadocJar()
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}
ext {
    vavrVersion = "0.10.3"
}

dependencies {
    implementation("io.vavr:vavr:$vavrVersion")
    implementation("org.hibernate.validator:hibernate-validator:7.0.1.Final")
    implementation("jakarta.validation:jakarta.validation-api:3.0.0")
    implementation("jakarta.el:jakarta.el-api:5.0.1")
    implementation("org.glassfish:jakarta.el:4.0.2")
    testImplementation(platform('org.junit:junit-bom:5.9.1'))
    testImplementation('org.junit.jupiter:junit-jupiter')
    testImplementation("org.assertj:assertj-core:3.10.0")
}

test {
    testLogging {
        events "PASSED", "SKIPPED", "FAILED", "STANDARD_OUT", "STANDARD_ERROR"
    }
}

tasks.named('test') {
    useJUnitPlatform()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from(components.java)
            pom {
                name = "Jooq Async"
                description = "Reactive vertx integration with jooq"
                inceptionYear = "2020"
                url = "https://github.com/maif/functional-validation"
                licenses {
                    license {
                        name = "The Apache License, Version 2.0"
                        url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                        distribution = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                    }
                }
                developers {
                    developer {
                        id = 'alexandre.delegue'
                        name = 'Alexandre Del√®gue'
                        url = 'https://github.com/larousso'
                    }
                    developer {
                        id = 'benjamin.cavy'
                        name = 'Benjamin Cavy'
                        url = 'https://github.com/ptitFicus'
                    }
                }
                scm {
                    url = "https://github.com/maif/functional-validation"
                    connection = "scm:git:git://github.com/maif/functional-validation.git"
                    developerConnection = "scm:git:ssh://github.com/maif/functional-validation.git"
                }
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

nexusPublishing {
    repositories {
        sonatype {
            nexusUrl.set(uri("https://ossrh-staging-api.central.sonatype.com/service/local/"))
            snapshotRepositoryUrl.set(uri("https://central.sonatype.com/repository/maven-snapshots/"))
            username = System.env.SONATYPE_USERNAME
            password = System.env.SONATYPE_PASSWORD
        }
    }
}

release {
    tagTemplate = 'v${version}'
}
release.git.requireBranch.set('master')